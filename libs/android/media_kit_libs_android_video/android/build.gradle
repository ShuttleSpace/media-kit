import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

def Prop(String name, String defaultValue = null) {
    if (project.hasProperty(name)) {
        return project.findProperty(name)
    } else if (System.getenv(name) != null) {
        return System.getenv(name)
    } else {
        return defaultValue
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their MD5 checksums.
        def filesToDownload = [
            ["url": Prop("LIBMPV_URL_ARM64", "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.5/default-arm64-v8a.jar"), "md5": Prop("LIBMPV_MD5_ARM64", "5f521b08692d7fef73c5df9bcc00ca4d"), "destination": file("$buildDir/v1.1.5/default-arm64-v8a.jar")],
            ["url": Prop("LIBMPV_URL_ARMV7", "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.5/default-armeabi-v7a.jar"), "md5": Prp("LIBMPV_MD5_ARMV7", "08d500ca1116c13e9c1296cc6f2207b0"), "destination": file("$buildDir/v1.1.5/default-armeabi-v7a.jar")],
            ["url": Prop("LIBMPV_URL_X64", "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.5/default-x86_64.jar"), "md5": Prop("LIBMPV_MD5_X64", "0880d5fbc3ff0053409704617f54cb55"), "destination": file("$buildDir/v1.1.5/default-x86_64.jar")],
            ["url": Prop("LIBMPV_URL_X86", "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.5/default-x86.jar"), "md5": Prop("LIBMPV_MD5_X86", "f6f51aa42b30d747099506cdc3277352"), "destination": file("$buildDir/v1.1.5/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedMD5 != fileInfo.md5) {
                    destFile.delete()
                    println "MD5 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedMD5 != fileInfo.md5) {
                    throw new GradleException("MD5 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)